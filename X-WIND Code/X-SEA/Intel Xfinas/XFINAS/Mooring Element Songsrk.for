C	==================================================================
C	==================================================================
C	==================================================================
	SUBROUTINE CMCABLE(PROPM,PROPG,NODEX,SPCFR,S,COORD,EDIS,EDISI,RE,
     #                   MWG,FIN)
      IMPLICIT REAL*8 (A-H,O-Z)
      IMPLICIT INTEGER*4 (I-N)
C     ---------------------------------------------------------------------
C     CATENARY MOORING CABLE AUG2022
C     ---------------------------------------------------------------------
C
      COMMON /FLAG/ IFPRI,ISPRI,IFPLO,IFREF,IFEIG,ITASK,IFFLAG

      COMMON /ELEM/ NAME(2),ITYPE,ISTYP,NLOPT,MTMOD,NSINC,ITOLEY,
     1              NELE,NMPS,NGPS,NMP,NGP,NNM,NEX,NCO,NNF,NWG,NEFC,
     2              NPT,NWA,NWS,KEG,MEL,NNO,NEF,NELTOT,NMV

C	GRAVTITY DIRECTION ADDED BY SONGSAK MAR2006  
	COMMON /MGRAV/ NGRAV

C     EQUILIBRIUM ARCHIEVED FLAG TO STORE THE INITIAL CABLE FORCE
C     KEQAR = 0 EQUILIBRIUM IS NOT ARCHIEVED YET, THE CABLE INITIAL FORCE HAVE TO BE STORE AND UPDATE
C     KEQAR = 1 EQUILIBRIUM IS ARCHIEVED, NO NEED TO UPDATE THE CABLE INITIAL FORCE
      COMMON /EQARC/ KEQAR
      
      DIMENSION PROPM(1),PROPG(5),EDIS(6),EDISI(6)
      DIMENSION COORD(6),COD(6)
C
	DIMENSION HN(2,6),DISP(2,1),RE(6)
	DIMENSION S(21)


	DIMENSION STIF(6,6)
	DIMENSION FIN(1)
	DIMENSION SPCFR(1)

	DIMENSION SKG(6,6),SKR(6,6)

	DIMENSION FOC(4),FO1(4),FO2(4)
	DIMENSION VL(3),VH(3),VG(3),VT(3)
	DIMENSION TRANS(6,6),FCO(6)
	DIMENSION CSTIF(6,6)


C	--------------------------------
C	FOR LINEAR AND MATERIAL NONLINEAR ONLY - UPDATE THE ELEMENT NODAL COORDINATE BY DISP. TERMS
C     THERE IS INITIAL EQUILIBRIUM ITERATION OF LINEAR ANALYSIS TO ACCOUNTING FOR MILD NONLINEAR EFFECT 
C	--------------------------------
	IF(NLOPT.LE.1) THEN
	    DO I = 1,6
	        COORD(I) = COORD(I)+EDIS(I)
	    ENDDO
	ENDIF

C	--------------------------------
C	INITIALIZATION OF SOME VARIABLES
C	--------------------------------
	YOUNG = PROPM(1)

C	---------------------------------------
C	SET VALUES FOR LINEAR STRESS-STRAIN LAW 
C	INITIALISATION OF INTEGRATION RULE
C	---------------------------------------
C	
	AREA  = PROPG(2)

	IFOPT = INT(PROPG(4))  !IFOPT    1=INITIAL FORCE WILL ADD TO LOAD VECTOR    0=INITIAL FORCE WILL NOT ADD TO LOAD VECTOR FEB09

	TDEN  = PROPM(5)  !MASS DENSITY OF CABLE SECTION
	WDEN  = PROPM(6)  !UNIT WEIGHT
      
      BDEN  = PROPM(7)  !WATER DENSITY (N)
      
      CDEN  = WDEN-BDEN !WEIGHT OF CABLE AFTER CANCEL WITH BOUYANCY 

	PDEN  = CDEN*AREA !CDEN*AREA  !WEIGHT PER UNIT LENGTH

	DX = COORD(4) - COORD(1)
	DY = COORD(5) - COORD(2)
	DZ = COORD(6) - COORD(3)

	TOL = 1.0E-8
	IF(ABS(DX).EQ.0.0D0) DX = TOL
	IF(ABS(DY).EQ.0.0D0) DY = TOL
	IF(ABS(DZ).EQ.0.0D0) DZ = TOL
	IF(ABS(DX).LT.TOL) DX = TOL*DX/ABS(DX)
	IF(ABS(DY).LT.TOL) DY = TOL*DY/ABS(DY)
	IF(ABS(DZ).LT.TOL) DZ = TOL*DZ/ABS(DZ)

	ELN		= SQRT(DX*DX + DY*DY + DZ*DZ)


C	------------------------------------------------
C	FORM MASS MATRIX
C	------------------------------------------------
	IF (ITASK.NE.5) GOTO 50
	SSAM = TDEN*AREA*ELN
	IMASS = 1
	IF (IMASS.EQ.1) THEN
C	  LUMPED MASS MATRIX
C	  ------------------
	  FACT = SSAM/2.0D0
	  S( 1) = FACT
	  S( 7) = FACT
	  S(12) = FACT
	  S(16) = FACT
	  S(19) = FACT
	  S(21) = FACT
	ELSE
C	  CONSISTENT MASS MATRIX
C	  ----------------------
	  FACT  = SSAM/3.0D0
	  S( 1) = FACT
	  S( 7) = FACT
	  S(12) = FACT
	  S(16) = FACT
	  S(19) = FACT
	  S(21) = FACT
	  FACT  = SSAM/6.0D0
	  S( 4) = FACT
	  S(10) = FACT
	  S(15) = FACT
	ENDIF
	RETURN
C	------------------------------------------------
50	CONTINUE 
C	------------------------------------------------
      
      
C     --------------------------------------------
C     GEOMETRY ANALYSIS OF CABLE PLANE 
C     --------------------------------------------
	VL(1:3) = [DX,DY,DZ]

	VH = VL
	VH(NGRAV) = 0.0D0
	CALL SCALEN (VH,VH,DUM,3)
	VG(1:3)   = 0.0D0
	VG(NGRAV) = 1.0D0

	CALL VECPRD (VH,VG,VT)

	CALL SCAPRD (VL,VH,Z,3)
	CALL SCAPRD (VL,VG,T,3)

	T = COORD(NGRAV+3) - COORD(NGRAV)
C     --------------------------------------------      
      
      

	TRANS = 0.0D0
	TRANS(1,1:3) = VH(1:3)
	TRANS(2,1:3) = VG(1:3)
	TRANS(3,1:3) = VT(1:3)
	TRANS(4,4:6) = VH(1:3)
	TRANS(5,4:6) = VG(1:3)
	TRANS(6,4:6) = VT(1:3)

	FHOR  = PROPG(3) !INITIAL HORIZONTAL FORCE FROM USER INPUT
	CLNOI = PROPG(5) !UNSTRETCHED LENGTH FROM USER INPUT
      
	CLNO  = SPCFR(9) !THIS ARRAY STORE THE INITIAL LENGTH
	IF(CLNO.EQ.0.0) THEN
          IF (CLNOI.NE.0.0) THEN
              CLNO = CLNOI
          ELSE
	        CALL CNINLEN(ELN,Z,T,AREA,YOUNG,PDEN,FHOR,CLNO)
          ENDIF
	    SPCFR(9) = CLNO
	ENDIF

C     FORCE ON CABLE END POINTs
	CALL PCAFX2 (Z,T,AREA,YOUNG,CLNO,PDEN,FOC)

C     VARIATION OF NODAL FORCE FOR ESTABLISHING STIFFNESS
	ALPHA = 1.0E-6
	H = Z+ALPHA
	V = T
	CALL PCAFX2 (H,V,AREA,YOUNG,CLNO,PDEN,FO1)

	V = T+ALPHA
	H = Z
	CALL PCAFX2 (H,V,AREA,YOUNG,CLNO,PDEN,FO2)

	A1 = (FO1(3)-FOC(3))/(ALPHA)
	A2 = (FO1(4)-FOC(4))/(ALPHA)
	A3 = (FO2(3)-FOC(3))/(ALPHA)
	A4 = (FO2(4)-FOC(4))/(ALPHA)

C     TRANSFORM FORCE FROM CABLE PLANE TO GLOBAL COOR. SYSTEM
	FCO = 0.0D0
	FCO(1:2) = FOC(1:2)
	FCO(4:5) = FOC(3:4)
	RE = MATMUL(TRANSPOSE(TRANS),FCO)
      
C     STORE INITIAL FORCE FOR FUTURE USING
	IF(KEQAR.EQ.0) THEN
	    SPCFR(2:7) = RE(1:6) !STORE THE INITIAL FORCES SINCE EQUILIBRIUM STATE IS BEING ARCHIEVED
      ENDIF
      
C     REMOVE INITIAL FORCE FROM ELEMENT LOAD VECTOR IF NEEDED (IFOPT = 1)
      IF(IFOPT.EQ.0) RE(1:6) = RE(1:6) - SPCFR(2:7)


	CSTIF = 0.0D0

	CSTIF(1,1) = A1
	CSTIF(2,1) = A2
	CSTIF(1,2) = A3
	CSTIF(2,2) = A4

	CSTIF(4,4) = A1
	CSTIF(5,4) = A2
	CSTIF(4,5) = A3
	CSTIF(5,5) = A4

	CSTIF(1,4) = -A1
	CSTIF(2,4) = -A2
	CSTIF(1,5) = -A3
	CSTIF(2,5) = -A4

	CSTIF(4,1) = -A1
	CSTIF(5,1) = -A2
	CSTIF(4,2) = -A3
	CSTIF(5,2) = -A4

C     OBTAIN MAX VALUSE OF DIAGONAL STIFFNESS
      SMAX = 0.0D0
	DO I = 1,6
	IF(ABS(CSTIF(I,I)).GT.SMAX) SMAX = ABS(CSTIF(I,I))
      ENDDO

C     ASSIGN ARTIFICIAL STIFFNESS FOR UNSTABLE DOF.
	TOL = 1.0e-6
	DO I = 1,6
	IF(ABS(CSTIF(I,I)).LT.TOL) CSTIF(I,I) = CSTIF(I,I) + SMAX
	ENDDO

	SKR = MATMUL(TRANSPOSE(TRANS),MATMUL(CSTIF,TRANS))

	KK = 1
	DO I = 1,6
	FIN(I) = RE(I)
	DO J = I,6
	S(KK) = SKR(I,J)
	KK = KK+1
	ENDDO
	ENDDO

C	STORE THE HORIZONTAL FORCE OF CABLE
	SPCFR(1) = ABS(FOC(1))


	RETURN


      END
C